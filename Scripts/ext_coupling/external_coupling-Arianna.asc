#===============================================
#---- INIT GLOBAL VARS
ON_EVENT "AppInitialized"
{
EXECUTE file:("db:\\ASC_HttpRequestDll.asc")
EXECUTE file:("db:\\ASC_GlobalFunctions_Conversion.asc")
EXECUTE file:("db:\\ASC_GlobalFunctions_JSON.asc")
EXECUTE file:("db:\\ASC_RandomDistributions.asc")
EXECUTE file:("db:\\translator.asc")
EXECUTE file:("db:\\mapKeySetFunctions_v2.asc")
#EXECUTE file:("db:\\reverse.asc")
EXECUTE file:("db:\\PIM_TO_PSM.asc")
EXECUTE file:("db:\\PSM_TO_FILE.asc")
EXECUTE file:("db:\\PSM_TYPES.asc")
EXECUTE file:("db:\\operation.asc")
EXECUTE file:("db:\\azure_dashboard_creation.asc")
EXECUTE file:("db:\\iot_scenario_creation.asc")
EXECUTE file:("db:\\models_from_azure.asc")

# imported from Davide's work
EXECUTE file:("db:\\auth.asc")
EXECUTE file:("db:\\DeviceSpecificationModel.asc")
EXECUTE file:("db:\\Export.asc")
EXECUTE file:("db:\\ExportLogicThingsBoard.asc")
EXECUTE file:("db:\\ExportScenarioThingsBoard.asc")
EXECUTE file:("db:\\ImportRuleChain.asc")
EXECUTE file:("db:\\ImportThingsBoard.asc")
EXECUTE file:("db:\\ImportLosant.asc")
}

ON_EVENT "AfterEditAttributeValue" {
 SETG objId:(instid)
 SETG attrId:(attrid)   
 EXECUTE file:("db:\\editattr.asc")
 CC "Core" GET_ATTR_NAME attrid:(attrid)
 IF (attrname = "Service") {
   ADD_VALUE objID:(objId)
 }
}

ON_EVENT "AfterCreateModelingNode" {
 CC "Core" GET_ATTR_VAL objid:(modelid) attrname:("Model Type")
 IF (val = "PSM") {
  CC "Core" GET_ATTR_VAL objid:(modelid) attrname:("Platform")
  ELEMENT_PSM platform:(val) objID:(objid)
 }
}

ON_EVENT "AfterCreateModelingConnector" {

 SET id_relationclassid: (classid)
 SET id_frominstanceid: (fromobjid)
 SET id_toinstanceid: (toobjid)
 SET id_model: (modelid)
 SET id_conn: (objid)

 CC "Core" GET_CLASS_NAME classid: (id_relationclassid)
 SET str_classname: (classname)

 CC "Core" GET_CONNECTORS objid:(id_toinstanceid) in
 SETL str_objids:(objids)

 # se ho piu' di un connettore dei miei constrain tipi lo blocco

 SETL str_ool:"b"
 FOR str_objid in:(str_objids) sep:" " {
  CC "Core" GET_CLASS_ID objid:(VAL str_objid)    
  CC "Core" GET_CLASS_NAME classid:(classid)
    
  IF (classname = "Domain" OR classname = "SpecialCase" OR classname = "Mandatory" OR classname = "Optional" OR classname = "AND" OR classname = "OR" OR classname = "XSSA" OR classname = "XSA" OR classname = "OSA") {
   IF (str_ool = "b") {
    SETL str_ool:"a"
   }
   ELSE {
    CC "Core" DELETE_CONNECTOR modelid:(id_model) objid:(id_conn)
    EXIT -1
   }
  }
 }

 # Devo controllare che posso inserirlo in questo punto
 CC "Core" GET_CONNECTORS objid:(id_frominstanceid) out
 SETL str_objids:(objids)
 
 FOR str_objid in:(str_objids) sep:" " {
  CC "Core" GET_CLASS_ID objid:(VAL str_objid)    
  CC "Core" GET_CLASS_NAME classid:(classid)
     
  IF (str_classname = "AND" OR str_classname = "OR" OR str_classname = "XSSA" OR str_classname = "XSA" OR str_classname = "OSA") {
   IF (classname != str_classname) {
    IF (classname = "DOInput" OR classname = "DOOutput") {

    } ELSE {
     CC "Core" DELETE_CONNECTOR modelid:(id_model) objid:(id_conn)
     EXIT -1
    }
   }
  }

  IF (str_classname = "Mandatory" OR str_classname = "Optional" OR str_classname = "Domain" OR str_classname = "SpecialCase") {
   IF (classname = "OR" OR classname = "AND" OR classname = "XSSA" OR classname = "XSA" OR classname = "OSA") {
    CC "Core" DELETE_CONNECTOR modelid:(id_model) objid:(id_conn)
    EXIT -1
   }
  }
 }
}

ON_EVENT "ActivateModelWindow" {
 CC "Modeling" GET_ACT_MODEL
 CC "Core" GET_MODEL_MODELTYPE modelid:(modelid)
 IF (modeltype = "Feature Operation Model")
 {
    CC "Application" SET_MENU_ITEM_ENABLED component:"modeling" item:("Generate\tDevices operations") enabled:0
 } ELSE {
    CC "Application" SET_MENU_ITEM_ENABLED component:"modeling" item:("Generate\tDevices operations") enabled:1
 }
}

ITEM "Send to BPMN editor" modeling:"Communicate with BPMN" 
EXPORT_TO_BPMN

ITEM "Import from BPMN editor" modeling:"Communicate with BPMN" 
IMPORT_FEATURE_PSM

ITEM "Generate Platform File" modeling:"PSM to code" sub-of:"Generate source files"
#EXECUTE file:("C:\\Users\\39389\\Desktop\\Laurea\\GithubRep\\Floware-X-IoT-library\\PSM_TO_FILE.asc")
PSM_TO_FILE

ITEM "Azure dashboard creation" modeling:"PSM to code" sub-of:"Deploy"
AZURE_DASHBOARD_CREATION

ITEM "Thingsboard" modeling:"PSM to code" sub-of:"Deploy"
EXPORT_THINGSBOARD

ITEM "Losant" modeling:"PSM to code" sub-of:"Deploy"
EXPORT_LOSANT

ITEM "Send PSM to FloWare Server" modeling:"PSM to code" sub-of:"Deploy"
SEND_FLOWARE_SERVER



ITEM "Export Feature Model json" modeling:"PSM to code" sub-of:"Generate source files"
TRANSLATE_PSM_TO_FILE

ITEM "Feature Model Selection" modeling:"PIM to PSM"
TRANSLATE_PIM_TO_PSM

ITEM "Azure" modeling:"PIM to PSM" sub-of:"Select platform"
PRESET_PSM platform:("Azure")

ITEM "Losant" modeling:"PIM to PSM" sub-of:"Select platform"
PRESET_PSM platform:("Losant")

ITEM "Thingsboard" modeling:"PIM to PSM" sub-of:"Select platform"
PRESET_PSM platform:("ThingsBoard")

ITEM "Devices operations" modeling:"Generate"
GENERATE_OPERATIONS

ITEM "Azure" modeling:"Generate" sub-of:"Model from platform"
MODELS_FROM_AZURE

ITEM "Losant" modeling:"Generate" sub-of:"Model from platform" 
IMPORT_LOSANT

ITEM "Thingsboard" modeling:"Generate" sub-of:"Model from platform" 
IMPORT_FROM_THINGSBOARD

ITEM "IoT Scenario and Logic" modeling:"IoT View Creation"
#EXECUTE file:("C:\\Users\\39389\\Desktop\\Laurea\\GithubRep\\Floware-X-IoT-library\\iot_scenario_creation.asc")
GENERATE_SCENARIO

ITEM "Device specification model" modeling:"Generate"
CREATE_DEVICE_SPECIFICATION_MODEL


